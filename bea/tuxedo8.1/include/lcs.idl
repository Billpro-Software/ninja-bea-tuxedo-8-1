/*	Copyright (c) 1997 BEA Systems, Inc.
	All rights reserved

	THIS IS UNPUBLISHED PROPRIETARY
	SOURCE CODE OF BEA Systems, Inc.
	The copyright notice above does not
	evidence any actual or intended
	publication of such source code.
*/
/* #ident	"@(#) objtm/idl/lcs.idl	$Revision: 1.9 $" */

#ifndef _OMG_LCS_IDL
#define _OMG_LCS_IDL

#include <ns.idl>

#ifdef _BLDJDKJAR
#pragma prefix "omg.org"
#endif
module CosLifeCycle {
#pragma prefix "omg.org/CosLifeCycle"

	typedef CosNaming::Name Key;
	typedef Object _Factory;
	typedef sequence <_Factory> Factories;
	typedef struct NVP {
		CosNaming::Istring name;
		any value;
	} NameValuePair;
	typedef sequence<NameValuePair> Criteria;

	exception NoFactory { Key search_key; };
	exception NotCopyable { string reason; };
	exception NotMovable { string reason; };
	exception NotRemovable { string reason; };
	exception InvalidCriteria { Criteria invalid_criteria; };
	exception CannotMeetCriteria { Criteria unmet_criteria; };
	exception NotImplemented { };

	interface FactoryFinder {
		Factories find_factories(in Key factory_key)
			raises (NoFactory);
	};

	interface LifeCycleObject {
		LifeCycleObject copy(in FactoryFinder there, in Criteria the_criteria)
			raises (NoFactory, NotCopyable, InvalidCriteria, CannotMeetCriteria, NotImplemented);
		void move(in FactoryFinder there, in Criteria the_criteria)
			raises (NoFactory, NotMovable, InvalidCriteria, CannotMeetCriteria, NotImplemented);
		void remove()
			raises (NotRemovable, NotImplemented);
	};

	interface GenericFactory {
		boolean _supports(in Key k)
			raises (NotImplemented);
		Object create_object(in Key k, in Criteria the_criteria)
			raises (NoFactory, InvalidCriteria, CannotMeetCriteria, NotImplemented);
	};
};
#ifndef JDKORB
#pragma ID CosLifeCycle "IDL:omg.org/CosLifeCycle:1.0"
#endif

#endif /* _OMG_LCS_IDL */
